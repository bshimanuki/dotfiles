set nocompatible
filetype off

if has('unix')
	runtime debian.vim
	let g:vimpath=$HOME.'/.vim'
endif

if has('win32')
	let g:vimpath=$HOME.'/vimfiles'
endif

function PluginEnabled(plugin)
	let l:plugin_path=g:vimpath.'/plugged/'.a:plugin
	return exists('g:plugs') && has_key(g:plugs, a:plugin) && !empty(glob(l:plugin_path.'/.git'))
endfunction

" vim-polyglot
let g:polyglot_disabled = ['sensible']
let g:NERDDelimiterMap={} " workaround for broken caddyfile ftplugin

" vim-plug
if !filereadable(g:vimpath.'/autoload/plug.vim')
	exec 'silent !wget --directory-prefix="'.g:vimpath.'/autoload/" '
				\ .'https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim'
	if v:shell_error==0
		autocmd VimEnter * echomsg 'vim-plug installed. Run :PlugInstall to install plugins.'
	else
		autocmd VimEnter * echomsg 'vim-plug could not be installed.'
		let l:vim_plug_not_installed=1
	endif
endif
if !exists('l:vim_plug_not_installed')
	call plug#begin(g:vimpath.'/plugged')
	Plug 'Lokaltog/vim-easymotion'
	Plug 'bkad/CamelCaseMotion'
	Plug 'embear/vim-localvimrc'
	Plug 'gerw/vim-latex-suite', {'for': ['context', 'plaintex', 'tex']}
	Plug 'jpalardy/vim-slime'
	Plug 'luochen1990/rainbow'
	Plug 'majutsushi/tagbar', {'on': ['TagbarOpen', 'TagbarToggle']}
	Plug 'scrooloose/nerdcommenter'
	Plug 'scrooloose/nerdtree'
	Plug 'sheerun/vim-polyglot'
	Plug 'terryma/vim-multiple-cursors'
	Plug 'tpope/vim-fugitive'
	Plug 'tpope/vim-rhubarb'
	Plug 'tpope/vim-sleuth'
	Plug 'tpope/vim-surround'
	Plug 'tpope/vim-unimpaired'
	Plug 'vim-airline/vim-airline'
	Plug 'vim-airline/vim-airline-themes'
	if has('unix') && !has('win32unix')
		Plug 'junegunn/fzf', {'do': './install --bin'}
		Plug 'junegunn/fzf.vim'
		Plug 'ycm-core/YouCompleteMe'
	else
		Plug 'kien/ctrlp.vim'
	endif
	call plug#end()
endif

" Other Plugins
runtime macros/matchit.vim

if PluginEnabled('vim-airline')
	if has('gui_running') || &t_Co==256
		let g:airline_theme='bubblegum'
	else
		let g:airline_theme='monochrome'
	endif
	let g:airline#extensions#tabline#enabled=1
	" let g:airline_powerline_fonts=1
	if !exists('g:airline_symbols')
		let g:airline_symbols={}
	endif
	let g:airline_symbols.colnr = ' „èá:'
	let g:airline_symbols.maxlinenr=''
	let g:airline_section_y='' " turn off file encoding, file format
endif

if PluginEnabled('CamelCaseMotion')
	call camelcasemotion#CreateMotionMappings(',')
endif

let s:exe_ext = 'exe|so|dll'
let s:compiled_ext = 'd|o|pdf|py[cdo]'
let s:img_ext = 'gif|jpg|png'
let s:data_ext = 'npy|npz|onnx|pb|pkl|pt'
let s:tex_ext = 'aux|bbl|bcf|blg|brf|fdb_latexmk|fls|idx|ilg|lof|lol|lot|pre|synctex\.gz|synctex\.gz\(busy\)|toc|x\.gnuplot' " exclude: log
let s:file_ext = join([s:exe_ext, s:compiled_ext, s:img_ext, s:data_ext, s:tex_ext], '|')
let s:dir_ext = 'git|hg|svn'
if PluginEnabled('ctrlp.vim') && !(PluginEnabled('fzf') && PluginEnabled('fzf.vim'))
	let g:ctrlp_working_path_mode='a'
	let g:ctrlp_custom_ignore = {
				\ 'dir':  '\v[\/]\.(' . s:dir_ext . ')$',
				\ 'file': '\v\.(' . s:file_ext . ')$',
				\ }
	let g:ctrlp_user_command = ['.git/', 'cd %s && git ls-files . -oc --exclude-standard | grep -v -E "\.(' . s:file_ext . ')$"']
endif

if PluginEnabled('vim-easymotion')
	noremap <Plug>(easymotion-prefix) <Nop>
endif

if PluginEnabled('fzf') && PluginEnabled('fzf.vim')
	let s:fzf_dir_ext = substitute(s:dir_ext, '|', ' -o -path \\*/.', 'g')
	let s:fzf_find_cmd = 'find -L $dir \( -path \*/.' . s:fzf_dir_ext .  ' \) -prune -o -type f -printf "%P\n" 2> /dev/null'
	let s:fzf_git_gitignore_cmd = 'git check-ignore -q ${dir:-' . getcwd() . '} 2> /dev/null'
	let s:fzf_git_lsfiles_cmd = 'git ls-files $dir -oc --exclude-standard 2> /dev/null'
	let s:fzf_grep_cmd = '(grep -v -E "\.(' . s:file_ext . ')$" || [[ $? == 1 ]])'
	let s:fzf_cmd = '(' . s:fzf_git_gitignore_cmd . ' ; [ $? -eq 1 ] && ' . s:fzf_git_lsfiles_cmd . ' || ' . s:fzf_find_cmd . ') | ' . s:fzf_grep_cmd
	function! Fzfvimfiles()
		if has('gui')
			return fzf#vim#files(getcwd(), {'source': s:fzf_cmd, 'sink': 'drop'})
		else
			return fzf#vim#files(getcwd(), {'source': s:fzf_cmd})
	endfunction
	nnoremap <silent><C-p> :call Fzfvimfiles()<CR>
	nnoremap <silent><C-f> :silent Buffers<CR>
	nnoremap <silent>g[ :silent Lines<CR>
	nnoremap <silent>g/ :silent BLines<CR>
	nnoremap <silent>g9 :silent Rg<CR>
endif

if PluginEnabled('vim-fugitive')
	nnoremap <silent><leader>g :silent Ggrep! -w <cword><CR>:copen<CR>:redraw!<CR>
	nnoremap <silent><leader>bb :exec line('.') . 'GBrowse!'<CR>
	vnoremap <silent><leader>bb :GBrowse!<CR>
	nnoremap <silent><leader>bf :GBrowse!<CR>
	vnoremap <silent><leader>bf <Esc>:GBrowse!<CR>gv
	nnoremap <silent><leader>bo :exec line('.') . 'GBrowse'<CR>
	vnoremap <silent><leader>bo :GBrowse<CR>
endif

if PluginEnabled('vim-latex-suite')
	let g:tex_flavor='latex'
	let g:Tex_DefaultTargetFormat='pdf'
	" imap <C-f> <Plug>IMAP_JumpForward
	" map <C-f> <Plug>IMAP_JumpForward
endif

if PluginEnabled('vim-localvimrc')
	" use `if g:localvimrc_sourced_once_for_file | finish | endif`
	" as an include guard
	let g:localvimrc_event=['BufNewFile', 'BufReadPre', 'BufWinEnter']
	let g:localvimrc_persistent=1
	let g:localvimrc_persistence_file=g:vimpath.'/.localvimrc_persistent'
endif

if PluginEnabled('nerdcommenter')
	let g:NERDSpaceDelims=1
	let g:NERDCustomDelimiters={
		\ 'c':{'left':'//'},
		\ 'python':{'left':'#'},
		\} " requires C99
endif

if PluginEnabled('nerdtree')
	nnoremap <leader>n :NERDTreeToggle<CR>
endif

if PluginEnabled('rainbow')
	let s:paren_colors=['red', 'darkcyan', 'lightyellow', 'darkgreen', 'lightblue', 'brown', 'darkmagenta', 'gray']
	let g:rainbow_active=1
	let g:rainbow_conf={
		\ 'guifgs': s:paren_colors,
		\ 'ctermfgs': s:paren_colors,
		\ 'operators': '_,_',
		\ 'parentheses': ['start=/(/ end=/)/ fold', 'start=/\[/ end=/\]/ fold', 'start=/{/ end=/}/ fold'],
		\ 'separately': {
		\  '*': 0,
		\  'clojure': {},
		\  'lisp': {},
		\  'scheme': {},
		\ }
		\}
endif

if PluginEnabled('vim-slime')
	let g:slime_target='tmux'
	let g:slime_default_config={'socket_name': 'default', 'target_pane': '0'}
	let g:slime_dont_ask_default=1
	let g:slime_no_mappings=1
	" let g:slime_python_ipython=1
	xmap <leader>s <Plug>SlimeRegionSend
	nmap <leader>s <Plug>SlimeMotionSend
	nmap <leader>ss <Plug>SlimeParagraphSend
	nmap <leader>sv <Plug>SlimeConfig
endif

if PluginEnabled('tagbar')
	let g:tagbar_sort=0
	nnoremap <leader>te :TagbarOpen fj<CR>
endif

"" YouCompleteMe
let g:ycm_python_interpreter_path='' " custom
let g:ycm_python_system_path='' " custom
let g:ycm_compilation_database_directory='' " custom, suffix directories to search
let g:ycm_confirm_extra_conf=1
let g:ycm_extra_conf_globlist=[]
let g:ycm_extra_conf_vim_data=[
			\ 'g:ycm_python_interpreter_path',
			\ 'g:ycm_python_system_path',
			\ 'g:ycm_compilation_database_directory',
			\ 'v:version',
			\ '&filetype',
			\]
let g:ycm_global_ycm_extra_conf=g:vimpath.'/.ycm_extra_conf.py'
let g:ycm_disable_signature_help=1
let g:ycm_always_populate_location_list=1
let g:ycm_auto_hover=''
let g:ycm_seed_identifiers_with_syntax=1
let g:ycm_disable_for_files_larger_than_kb=10000
let g:ycm_collect_identifiers_from_tag_files=1
let g:ycm_collect_identifiers_from_comments_and_strings=1
let g:ycm_filetype_blacklist={
	\ 'tagbar' : 1,
	\ 'qf' : 1,
	\ 'notes' : 1,
	\ 'markdown' : 1,
	\ 'unite' : 1,
	\ 'text' : 1,
	\ 'vimwiki' : 1,
	\ 'pandoc' : 1,
	\ 'infolog' : 1,
	\ 'mail' : 1,
	\ 'tex' : 1
	\}
nnoremap <leader>f :YcmCompleter GoTo<CR>
nnoremap <leader>r :YcmCompleter GoToReferences<CR>
nnoremap <leader>h :YcmCompleter GetDoc<CR>
nnoremap <leader>x :YcmCompleter FixIt<CR>

" Config
if has("patch-7.4.338")
	let &showbreak='....'
	set breakindent
	set breakindentopt=shift:0
endif
set backupcopy=yes
set fileformats=unix,dos
set nofixendofline
set number
filetype plugin indent on
syntax on
set formatoptions+=ro
set cinoptions=:0,ls,g0,N-s,(s,u0,m1
set showcmd cmdheight=2
set tabstop=2 shiftwidth=2
set nojoinspaces
set nofoldenable foldmethod=indent " foldmethod=syntax slows vim considerably
set linebreak
set lazyredraw
set scrolloff=8 sidescrolloff=8
set modeline
set visualbell t_vb=
autocmd GUIEnter * set t_vb=
set ruler
set mouse=
set backspace=indent,eol,start
set display=lastline,uhex
set whichwrap+=<,>,[,]
set wildmenu wildmode=list,full
let mapleader="\\"
set notimeout ttimeout ttimeoutlen=0
set splitbelow splitright
set diffopt+=vertical
set history=100
set directory=~/tmp//,~/tmp,/var/tmp,.,/tmp

" Searching
set incsearch hlsearch
nnoremap <silent><Space> :noh<CR>
vnoremap <silent><Space> <Esc>:noh<CR>gv

" Font, Colorscheme
if has('gui_running')
	colorscheme torte
	set cursorline
	highlight CursorLine guibg=gray20
else
	colorscheme koehler
	if &t_Co==256
		set cursorline
		highlight CursorLine cterm=NONE ctermbg=236 " 236=Gray19
		highlight CursorLineNr cterm=NONE ctermbg=236 " 236=Gray19
		highlight LineNr ctermbg=238 " 238=Gray27
	endif
endif
if has('unix')
	set guifont=Lucida\ Console\ 11
endif
if has('win32')
	set guifont=Lucida_Console:h11
endif
highlight Cursor guibg=RoyalBlue ctermbg=lightblue
highlight MatchParen guibg=darkblue guifg=cyan ctermbg=darkblue ctermfg=cyan
highlight Folded guibg=Gray11 guifg=darkcyan ctermbg=234 ctermfg=darkcyan " 234=Gray11
highlight DiffDelete ctermfg=lightblue
highlight Todo ctermfg=yellow
if has('win32unix')
	let &t_ti.="\e[1 q"
	let &t_SI.="\e[5 q"
	let &t_EI.="\e[1 q"
	let &t_te.="\e[0 q"
endif

" Spell
set spelllang=en_us
exec 'set spellfile='.g:vimpath.'/spell/en.utf-8.add'
highlight SpellBad ctermbg=darkred ctermfg=lightred
highlight SpellCap ctermbg=darkblue ctermfg=lightblue
highlight SpellRare ctermbg=darkmagenta ctermfg=lightmagenta
highlight SpellLocal ctermbg=darkblue ctermfg=lightblue

" Shell
set shellslash
set grepprg=grep\ -nH\ $*

" Tmux
if exists('$TMUX')
	let s:cwd=fnamemodify(".", ":p:~")
	let s:file=expand("%:.")
	if empty(s:file)
		let s:file="[No Name]"
	endif
	let &t_ti="\<Esc>]2;" . s:cwd . " > (VIM) " . s:file . "\<Esc>\\" . &t_ti
	set notitle
endif

" Remaps
"" Save and Quit
command! -bang Q q<bang>
command! -bang Qa qa<bang>
command! -bang QA qa<bang>
command! W w
command! Wq wq
command! WQ wq
"" Movement
noremap <Up> gk
noremap <Down> gj
noremap <Home> g<Home>
noremap <End> g<End>
nnoremap <C-h> <C-w>h
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k
nnoremap <C-l> <C-w>l
nnoremap gb :bn<CR>
nnoremap gB :bp<CR>
"" Function Movement
let s:function_match='^\(\w\(\n\=[^;]\)\{-\}\)\='
let s:delete_last_history=':<C-u>call histdel("/",-1)<CR> :let @/=histget("/",-1)<CR>'
noremap <expr> <Plug>DeleteLastHistory <SID>DeleteLastHistory()
exec "noremap <silent> [[ :call search('".s:function_match."{','sb')<CR>"
exec "noremap <silent> ]] :call search('".s:function_match."{','s')<CR>"
exec "noremap <silent> [] :call search('".s:function_match."}','sb')<CR>"
exec "noremap <silent> ][ :call search('".s:function_match."}','s')<CR>"
exec "vnoremap <silent> [[ ?".s:function_match."{<CR> " . s:delete_last_history . "gv^"
exec "vnoremap <silent> ]] /".s:function_match."{<CR> " . s:delete_last_history . "gv^"
exec "vnoremap <silent> [] ?".s:function_match."}<CR> " . s:delete_last_history . "gv^"
exec "vnoremap <silent> ][ /".s:function_match."}<CR> " . s:delete_last_history . "gv^"
"" Grep for word under cursor
""" recursive under cwd
nnoremap <silent> <expr> gy ":silent vimgrep /\\C\\<" . expand("<cword>") . "\\>/j **/*<CR>"
""" nonrecursive in same directory
nnoremap <silent> <expr> gY ":silent vimgrep /\\C\\<" . expand("<cword>") . "\\>/j %:p:h/*<CR>"
"" Layout
command! Bd b # | bd #
nnoremap <leader>tt :NERDTreeToggle<CR> <C-w><C-p> :TagbarToggle<CR>
nnoremap <leader>to :NERDTree<CR> <C-w><C-p> :TagbarOpen<CR>
nnoremap <leader>tc :NERDTreeClose<CR> <C-w><C-p> :TagbarClose<CR>

" Automatically open, but do not go to (if there are errors) the quickfix /
" location list window, or close it when is has become empty.
autocmd QuickFixCmdPost [^l]* nested belowright cwindow
autocmd QuickFixCmdPost    l* nested belowright lwindow
